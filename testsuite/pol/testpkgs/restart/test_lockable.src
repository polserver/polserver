use os;
use uo;
use polsys;
include "testutil";

var testrun:=CInt(GetEnvironmentVariable("POLCORE_TEST_RUN"));

program test_lockable()
  return 1;
endprogram

// save/load test for things in Lockable class
exported function load_save_lockable()
  var props:={
                {"Locked", {"locked",1}}
  };

  if (testrun == 1)
    var item := CreateItemAtLocation(10,11,-5, "restart_test_container",1,"britannia2");
    item.name:="load_save_lockable";
    if (!item)
      return ret_error($"failed to create item {item}");
    endif
    SetGlobalProperty("test_item_storage_lockable", item.serial);
    foreach prop in props
      item.set_member(prop[2][1],prop[2][2]);
    endforeach
  else

    var serial := GetGlobalProperty("test_item_storage_lockable");
    if (!serial)
      return ret_error("Global property not found");
    endif

    var item := SystemFindObjectBySerial(serial);
    if (!item)
      return ret_error($"Item with serial {serial:#x} does not exists: {item})");
    endif
    foreach prop in props
      var val:=item.get_member(prop[2][1]);
      if (val != prop[2][2])
        return ret_error($"Member {prop[2][1]} {prop[2][2]} != {val}");
      endif
    endforeach
  endif
  return 1;
endfunction
