use os;
use uo;
use polsys;
include "testutil";

var testrun:=CInt(GetEnvironmentVariable("POLCORE_TEST_RUN"));

program test_item()
  return 1;
endprogram

exported function test_saveonexit()
  if (testrun == 1)
    // Test setting SaveOnExit in item descriptor successfully changes item save behavior
    var desc := GetItemDescriptor(0xeed);
    desc.SaveOnExit := 0;
    var item := CreateItemAtLocation(55,55,0, desc);
    if (!item) 
      return ret_error($"Could not create should-not-save item in first server run: {item}");
    endif
    SetGlobalProperty("test_item_shouldNotExist", item.serial);

    // Change SaveOnExit to true, to enable saving the item
    desc.SaveOnExit := 1;
    var item2 := CreateItemAtLocation(55,55,0, desc);
    if (!item2) 
      return ret_error($"Could not create should-save item in first server run: {item2}");
    endif
    SetGlobalProperty("test_item_shouldExist", item2.serial);
  else
    // Check that the item that should not exist does not exist
    var serial := GetGlobalProperty("test_item_shouldNotExist");
    if (!serial)
      return ret_error("Global property test_item_shouldNotExist not found");
    endif

    var item := SystemFindObjectBySerial(serial);
    if (item)
      return ret_error($"Item with serial {item.serial:#x} erroneously exists: {item.desc} (objtype {item.objtype:#x}) at ({item.x}, {item.y}, {item.z})");
    endif

    // Check that the item that should exist does exist
    var serial2 := GetGlobalProperty("test_item_shouldExist");
    if (!serial2)
      return ret_error("Global property test_item_shouldExist not found");
    endif

    var item2 := SystemFindObjectBySerial(serial2);
    if (!item2)
      return ret_error($"Item with serial {serial2:#x} does not exists: {item2})");
    endif
  endif
  return 1;
endfunction

// save/load test for things in UObject
exported function load_save_uobject()
  var props:={
                   {"Name", {"name","load_save_uobject"}},
                {"Graphic", {"graphic",0x1234}},
                  {"Color", {"color",0xff0}},
                 {"Facing", {"facing",6}}, // after graphic or it gets resetted
//               {"Revision", {"",1}},
          {"FireResistMod", {"resist_fire_mod",1}},
          {"ColdResistMod", {"resist_cold_mod",2}},
        {"EnergyResistMod", {"resist_energy_mod",3}},
        {"PoisonResistMod", {"resist_poison_mod",4}},
      {"PhysicalResistMod", {"resist_physical_mod",5}},
          {"FireDamageMod", {"damage_fire_mod",6}},
          {"ColdDamageMod", {"damage_cold_mod",7}},
        {"EnergyDamageMod", {"damage_energy_mod",8}},
        {"PoisonDamageMod", {"damage_poison_mod",9}},
      {"PhysicalDamageMod", {"damage_physical_mod",10}},
    {"LowerReagentCostMod", {"lower_reagent_cost_mod",11}},
     {"DefenceIncreaseMod", {"defence_increase_mod",12}},
  {"DefenceIncreaseCapMod", {"defence_increase_cap_mod",13}},
       {"LowerManaCostMod", {"lower_mana_cost_mod",14}},
           {"HitChanceMod", {"hit_chance_mod",15}},
       {"FireResistCapMod", {"resist_fire_cap_mod",16}},
       {"ColdResistCapMod", {"resist_cold_cap_mod",17}},
     {"EnergyResistCapMod", {"resist_energy_cap_mod",18}},
   {"PhysicalResistCapMod", {"resist_physical_cap_mod",19}},
     {"PoisonResistCapMod", {"resist_poison_cap_mod",20}},
 {"SpellDamageIncreaseMod", {"spell_damage_increase_mod",21}},
       {"FasterCastingMod", {"faster_casting_mod",22}},
  {"FasterCastRecoveryMod", {"faster_cast_recovery_mod",23}},
                {"LuckMod", {"luck_mod",24}},
  {"SwingSpeedIncreaseMod", {"swing_speed_increase_mod",25}}
  };

  if (testrun == 1)
    var item := CreateItemAtLocation(10,11,-5, 0xeed,1,"britannia2");
    if (!item)
      return ret_error($"failed to create item {item}");
    endif
    SetGlobalProperty("test_item_storage_uobject", item.serial);
    foreach prop in props
      item.set_member(prop[2][1],prop[2][2]);
    endforeach
    item.setprop("test","testcprop");
  else

    var serial := GetGlobalProperty("test_item_storage_uobject");
    if (!serial)
      return ret_error("Global property not found");
    endif

    var item := SystemFindObjectBySerial(serial);
    if (!item)
      return ret_error($"Item with serial {serial:#x} does not exists: {item})");
    endif
    foreach prop in props
      var val:=item.get_member(prop[2][1]);
      if (val != prop[2][2])
        return ret_error($"Member {prop[2][1]} {prop[2][2]} != {val}");
      endif
    endforeach
    if (item.objtype != 0xeed)
      return ret_error($"wrong objtype {item.objtype:#x} != 0xeed");
    endif
    if (item.x != 10 || item.y != 11 || item.z != -5 || item.realm != "britannia2")
      return ret_error($"wrong pos {item.x},{item.y},{item.z},{item.realm} != 10,11,-5,britannia2");
    endif
    if (item.getprop("test") != "testcprop")
      return ret_error("failed to load cprop: "+item.getprop("test"));
    endif
  endif
  return 1;
endfunction

// save/load test for things in Item
exported function load_save_item()
  var props:={
                  {"Name", {"name","load_save_item"}},
//                 {"Layer", {"",1}},
               {"Movable", {"movable",1}},
             {"Invisible", {"invisible",1}},
                {"Cursed", {"cursed",1}},
//             {"Container", {"",1}},
           {"OnUseScript", {"usescript","myusescript"}},
           {"EquipScript", {"equipscript","myequipscript"}},
         {"UnequipScript", {"unequipscript","myunequipscript"}},
           {"SnoopScript", {"snoopscript","mysnoopscript"}},
               {"DecayAt", {"decayat",12345678}},
             {"SellPrice", {"sellprice",111}},
              {"BuyPrice", {"buyprice",110}},
                {"Newbie", {"newbie",1}},
               {"Insured", {"insured",1}},
             {"MaxHp_mod", {"maxhp_mod",200}},
                    {"Hp", {"hp",123}},
               {"Quality", {"quality",1.123}},
            {"NameSuffix", {"name_suffix","my_suffix"}},
                {"NoDrop", {"no_drop",1}}
  };

  if (testrun == 1)
    var item := CreateItemAtLocation(10,11,-5, 0xf3f,1234,"britannia2");
    if (!item)
      return ret_error($"failed to create item {item}");
    endif
    SetGlobalProperty("test_item_storage_item", item.serial);
    foreach prop in props
      item.set_member(prop[2][1],prop[2][2]);
    endforeach
  else

    var serial := GetGlobalProperty("test_item_storage_item");
    if (!serial)
      return ret_error("Global property not found");
    endif

    var item := SystemFindObjectBySerial(serial);
    if (!item)
      return ret_error($"Item with serial {serial:#x} does not exists: {item})");
    endif
    foreach prop in props
      var val:=item.get_member(prop[2][1]);
      if (val != prop[2][2])
        return ret_error($"Member {prop[2][1]} {prop[2][2]} != {val}");
      endif
    endforeach
    if (item.amount != 1234)
      return ret_error($"wrong amount {item.amount} != 1234");
    endif
  endif
  return 1;
endfunction

// save/load test for things in Item (descriptor only)
exported function load_save_item_descriptor()
  var desc := GetItemDescriptor("restart_test");
  desc.FireResist := 1;
  desc.ColdResist := 2;
  desc.EnergyResist := 3;
  desc.PoisonResist := 4;
  desc.PhysicalResist := 5;
  desc.FireDamage := 6;
  desc.ColdDamage := 7;
  desc.EnergyDamage := 8;
  desc.PoisonDamage := 9;
  desc.PhysicalDamage := 10;
  desc.LowerReagentCost := 11;
  desc.SpellDamageIncrease := 12;
  desc.FasterCasting := 13;
  desc.FasterCastRecovery := 14;
  desc.DefenceIncrease := 15;
  desc.DefenceIncreaseCap := 16;
  desc.LowerManaCost := 17;
  desc.FireResistCap := 18;
  desc.ColdResistCap := 19;
  desc.EnergyResistCap := 20;
  desc.PhysicalResistCap := 21;
  desc.PoisonResistCap := 22;
  desc.Luck := 23;
  desc.SwingSpeedIncrease := 24;
  var props:={
            {"FireResist", {"resist_fire", desc.FireResist}},
            {"ColdResist", {"resist_cold", desc.ColdResist}},
          {"EnergyResist", {"resist_energy", desc.EnergyResist}},
          {"PoisonResist", {"resist_poison", desc.PoisonResist}},
        {"PhysicalResist", {"resist_physical", desc.PhysicalResist}},
            {"FireDamage", {"damage_fire", desc.FireDamage}},
            {"ColdDamage", {"damage_cold", desc.ColdDamage}},
          {"EnergyDamage", {"damage_energy", desc.EnergyDamage}},
          {"PoisonDamage", {"damage_poison", desc.PoisonDamage}},
        {"PhysicalDamage", {"damage_physical", desc.PhysicalDamage}},
      {"LowerReagentCost", {"lower_reagent_cost", desc.LowerReagentCost}},
   {"SpellDamageIncrease", {"spell_damage_increase", desc.SpellDamageIncrease}},
         {"FasterCasting", {"faster_casting", desc.FasterCasting}},
    {"FasterCastRecovery", {"faster_cast_recovery", desc.FasterCastRecovery}},
       {"DefenceIncrease", {"defence_increase", desc.DefenceIncrease}},
    {"DefenceIncreaseCap", {"defence_increase_cap", desc.DefenceIncreaseCap}},
         {"LowerManaCost", {"lower_mana_cost", desc.LowerManaCost}},
         {"FireResistCap", {"resist_fire_cap", desc.FireResistCap}},
         {"ColdResistCap", {"resist_cold_cap", desc.ColdResistCap}},
       {"EnergyResistCap", {"resist_energy_cap", desc.EnergyResistCap}},
     {"PhysicalResistCap", {"resist_physical_cap", desc.PhysicalResistCap}},
       {"PoisonResistCap", {"resist_poison_cap", desc.PoisonResistCap}},
                  {"Luck", {"luck", desc.Luck}},
    {"SwingSpeedIncrease", {"swing_speed_increase", desc.SwingSpeedIncrease}}
  };
  if (testrun == 1)
    var item := CreateItemAtLocation(10,11,-5, desc,1,"britannia2");
    item.name:="load_save_item_descriptor";
    if (!item)
      return ret_error($"failed to create item {item}");
    endif
    SetGlobalProperty("test_item_storage_item_descriptor", item.serial);
  else

    var serial := GetGlobalProperty("test_item_storage_item_descriptor");
    if (!serial)
      return ret_error("Global property not found");
    endif

    var item := SystemFindObjectBySerial(serial);
    if (!item)
      return ret_error($"Item with serial {serial:#x} does not exists: {item})");
    endif
    foreach prop in props
      var val:=item.get_member(prop[2][1]);
      if (val != prop[2][2])
        return ret_error($"Member {prop[2][1]} {prop[2][2]} != {val}");
      endif
    endforeach
  endif
  return 1;
endfunction

// save/load test for things in Armor
exported function load_save_armor()
  var props:={
                {"AR_mod", {"ar_mod",12}},
           {"OnHitScript", {"onhitscript",":testrestart:myhitscript"}}
  };

  if (testrun == 1)
    var item := CreateItemAtLocation(10,11,-5, "restart_test_armor",1,"britannia2");
    item.name:="load_save_armor";
    if (!item)
      return ret_error($"failed to create item {item}");
    endif
    SetGlobalProperty("test_item_storage_armor", item.serial);
    foreach prop in props
      item.set_member(prop[2][1],prop[2][2]);
    endforeach
  else

    var serial := GetGlobalProperty("test_item_storage_armor");
    if (!serial)
      return ret_error("Global property not found");
    endif

    var item := SystemFindObjectBySerial(serial);
    if (!item)
      return ret_error($"Item with serial {serial:#x} does not exists: {item})");
    endif
    foreach prop in props
      var val:=item.get_member(prop[2][1]);
      if (val != prop[2][2])
        return ret_error($"Member {prop[2][1]} {prop[2][2]} != {val}");
      endif
    endforeach
  endif
  return 1;
endfunction

// save/load test for things in Weapon class
exported function load_save_weapon()
  var props:={
                {"dmg_mod", {"dmg_mod",1}},
              {"speed_mod", {"speed_mod",2}},
              {"HitScript", {"hitscript",":testrestart:myhitscript"}}
  };

  if (testrun == 1)
    var item := CreateItemAtLocation(10,11,-5, "restart_test_weapon",1,"britannia2");
    item.name:="load_save_weapon";
    if (!item)
      return ret_error($"failed to create item {item}");
    endif
    SetGlobalProperty("test_item_storage_weapon", item.serial);
    foreach prop in props
      item.set_member(prop[2][1],prop[2][2]);
    endforeach
  else

    var serial := GetGlobalProperty("test_item_storage_weapon");
    if (!serial)
      return ret_error("Global property not found");
    endif

    var item := SystemFindObjectBySerial(serial);
    if (!item)
      return ret_error($"Item with serial {serial:#x} does not exists: {item})");
    endif
    foreach prop in props
      var val:=item.get_member(prop[2][1]);
      if (val != prop[2][2])
        return ret_error($"Member {prop[2][1]} {prop[2][2]} != {val}");
      endif
    endforeach
  endif
  return 1;
endfunction
